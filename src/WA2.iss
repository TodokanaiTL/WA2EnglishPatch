#ifndef VERSION
#define VERSION     "1.0.0.0"
#endif

#define BINPATH     SourcePath + "..\bin"
#define EXTERNL     "external skipifsourcedoesntexist"
#define POSTINS     "postinstall skipifsilent"
#define WA2REGK     "Software\Leaf\WHITE ALBUM2"

#define NAME_NVL01  "The Idol Who Forgot How to Sing"
#define NAME_NVL02  "The Snow Melts and Until It Falls Again"

#define LINK_MV010  "https://cloud.disroot.org/s/sRAzdd4ccW7lTok/download"
#define LINK_MV020  "https://cloud.disroot.org/s/3ogQTfqSrShs92j/download"
#define LINK_MV070  "https://cloud.disroot.org/s/FTF5wxZXOlPdEQx/download"
#define LINK_MV080  "https://cloud.disroot.org/s/Cr7PYFTzDLiqWFg/download"
#define LINK_MV090  "https://cloud.disroot.org/s/b7DlHjPRgro8wyp/download"
#define LINK_MV100  "https://cloud.disroot.org/s/LGHAnj6m2Jjrann/download"
#define LINK_MV110  "https://cloud.disroot.org/s/Q6MczbGCdHrk6is/download"
#define LINK_MV120  "https://cloud.disroot.org/s/5dLNipmdF4mSdw8/download"
#define LINK_MV130  "https://cloud.disroot.org/s/QJaykSB7MwHZ4ct/download"
#define LINK_MV140  "https://cloud.disroot.org/s/jzqncXt7mSnfxTG/download"
#define LINK_MV200  "https://cloud.disroot.org/s/kDDSTHYUEi6wRP2/download"
#define LINK_MV210  "https://cloud.disroot.org/s/J2m7yRmEjH6doEN/download"
#define LINK_MV220  "https://cloud.disroot.org/s/YQbnCA46AZCisps/download"
#define LINK_MV230  "https://cloud.disroot.org/s/yqkXAXSgRJbGGgn/download"
#define LINK_D3DX9  "https://cloud.disroot.org/s/R3DDjypiQEFSkxP/download"
#define LINK_STTXT  "https://cloud.disroot.org/s/mYaEc8NNoyXm9Cy/download"
#define LINK_STFNT  "https://cloud.disroot.org/s/oxQbQfgtska8FtF/download"
#define LINK_NVL01  "https://cloud.disroot.org/s/Mq8Z6JctQNdJAcF/download"
#define LINK_NVL02  "https://cloud.disroot.org/s/gD6TJb7c4mCY5ym/download"

#define HASH_MV010  "ec47f6a5eaed9bf5ca497c6196d64fdeab89c2a211399f99ab5409fd3afcb7e8"
#define HASH_MV020  "4d28aa1d60f4fd893dbf8a35be6f7571c9682949a56ce3a2c7c521edc4ee7097"
#define HASH_MV070  "f89a8d5a2ed3b44336f08aec56faa68a101afae5ffe79ffabe082532bfbe3062"
#define HASH_MV080  "008a14ae1391675768e11279dbd9221bb7ef87ae98fb32bf796b42c14d02fb9c"
#define HASH_MV090  "b3f91ebe5068142a91643310f45b8b1db1a2bd7cd231bd24508ef319a4c626e2"
#define HASH_MV100  "a6938f8e3d6fddaa8f4ae129d0335f588d1494b60dfd41d739f5a64a7d95b2e6"
#define HASH_MV110  "b4131ec8b2f8c984d6361b71a126369343c127f7b66fd2cbace96f3af44ab4c0"
#define HASH_MV120  "eeea9c03352fbdeccccec1cf9d0244b9ec1b9cfff0146ceb7886f9117a9edba7"
#define HASH_MV130  "fbf718140ea8611c0051b486229fd0713f58188aa7adc79d358ae05bbd70fe9e"
#define HASH_MV140  "5bcffc26423370433d292094783952ce1cb36930ca5c1c3a211be240e9a39af3"
#define HASH_MV200  "5fbdc146e9027391e825829a9dfbf571d3b99a41d16e4b5a1b7207f6193b2510"
#define HASH_MV210  "089a904e2f31aea9fc9120c5474c9a7bb62b5876aab9169b4bfd685eb9b4d477"
#define HASH_MV220  "d3a615bedd4f98e4d1741c02c37c7aea7baefaad247bcda4effb041aa8e4f984"
#define HASH_MV230  "94f388e1b14e62c65c9fb86b20baf034e5607bc94cf23489b38d200444c01a3d"
#define HASH_D3DX9  "49c098a07cc8fea6be6aea7d1dfd973e83d9b434144b1584591e99e189178b0f"
#define HASH_STTXT  "0cc1babc7cb03b82074b1e891886bb6f888f1a12972a2b462847dba0df245eaf"
#define HASH_STFNT  "002b00fae25a6b564e86117e5c2e0ebb21b811ca697c648561aaa86e1a239a92"
#define HASH_NVL01  "1e5ccb135827b2afa359fd1d8346823412e1a20607213a3325c3b27a844ce38e"
#define HASH_NVL02  "500b1fc9ea84ec13671783a3a7179699509c2ed85243241a28b55b676083e5b8"

#define SIZE_MV010  "012320991"
#define SIZE_MV020  "087833729"
#define SIZE_MV070  "028081783"
#define SIZE_MV080  "094121818"
#define SIZE_MV090  "190228686"
#define SIZE_MV100  "062070235"
#define SIZE_MV110  "158473954"
#define SIZE_MV120  "181295703"
#define SIZE_MV130  "231926118"
#define SIZE_MV140  "273060767"
#define SIZE_MV200  "087814085"
#define SIZE_MV210  "211972721"
#define SIZE_MV220  "248811699"
#define SIZE_MV230  "241533509"
#define SIZE_D3DX9  "000243712"
#define SIZE_STTXT  "000025675"
#define SIZE_STFNT  "000065497"
#define SIZE_NVL01  "001253262"
#define SIZE_NVL02  "001085084"

[Setup]
; App info
AppId = {{89357994-3C15-4411-894D-A23CE3FF1AA1}
AppName = White Album 2 English
AppVersion = {#VERSION}
AppPublisher = Todokanai TL
AppCopyright = Copyright (C) 2017-2022, Todokanai TL
AppUpdatesURL = https://github.com/TodokanaiTL/WA2EnglishPatch/releases
AppPublisherURL = https://todokanaitl.github.io
AppSupportURL = https://discord.gg/5rrxEUN
VersionInfoDescription = White Album 2 English Patch
VersionInfoVersion = {#VERSION}

; Output
OutputDir = {#SourcePath}..\out
OutputBaseFilename = WA2_patch

; Included files
LicenseFile = {#SourcePath}..\LICENSE
InfoBeforeFile = {#SourcePath}..\res\Instructions.rtf
InfoAfterFile = {#SourcePath}..\res\ReleaseNotes.rtf
SetupIconFile = {#SourcePath}..\res\logo.ico
WizardImageFile = {#SourcePath}..\res\164x314.bmp
WizardSmallImageFile = {#SourcePath}..\res\55x55.bmp

; Compression
Compression = lzma2/ultra
LZMANumBlockThreads = 4
LZMAUseSeparateProcess = yes

; Installation
DirExistsWarning = no
DefaultDirName = {src}
UsePreviousAppDir = yes
AppendDefaultDirName = no
UsedUserAreasWarning = no
DisableProgramGroupPage = yes
AllowCancelDuringInstall = yes
ArchitecturesAllowed = x86 x64
EnableDirDoesntExistWarning = yes
ArchitecturesInstallIn64BitMode = x64

; Misc
Uninstallable = no
SetupLogging = yes
WizardStyle = modern
TimeStampsInUTC = yes
WizardSizePercent = 135
WizardImageStretch = no
WizardImageAlphaFormat = defined
DefaultDialogFontName = Lucida Sans Unicode

[Languages]
; Setup language
Name: "English"; MessagesFile: "compiler:Default.isl"

[InstallDelete]
; Delete obsolete files
Type: files;          Name: "{app}\ev000.pak"
Type: files;          Name: "{app}\ev150.pak"
Type: filesandordirs; Name: "{userdocs}\White Album 2 Patch Logs"

[Types]
; Installation types
Name: "default"; Description: "Default installation"
Name: "minimal"; Description: "Minimal installation"
Name: "custom";  Description: "Custom installation"; Flags: iscustom

[Components]
; Installation components
Name: "patch";        Description: "English patch";     Types: custom default minimal; Flags: fixed
Name: "subtitles";    Description: "Audio-only subs";   Types: custom default
Name: "videos";       Description: "Subbed videos";     Types: custom
Name: "videos\mv010"; Description: "mv010";             Types: custom default
Name: "videos\mv020"; Description: "mv020";             Types: custom
Name: "videos\mv070"; Description: "mv070";             Types: custom default
Name: "videos\mv080"; Description: "mv080";             Types: custom
Name: "videos\mv090"; Description: "mv090";             Types: custom
Name: "videos\mv100"; Description: "mv100";             Types: custom
Name: "videos\mv110"; Description: "mv110";             Types: custom
Name: "videos\mv120"; Description: "mv120";             Types: custom
Name: "videos\mv130"; Description: "mv130";             Types: custom
Name: "videos\mv140"; Description: "mv140";             Types: custom
Name: "videos\mv200"; Description: "mv200";             Types: custom
Name: "videos\mv210"; Description: "mv210";             Types: custom
Name: "videos\mv220"; Description: "mv220";             Types: custom
Name: "videos\mv230"; Description: "mv230";             Types: custom
Name: "novels";       Description: "Digital novels";    Types: custom

[Dirs]
; Directories
Name: "{app}\IC";        Components: videos
Name: "{app}\novels";    Components: novels
Name: "{app}\todokanai"; Components: subtitles

[Tasks]
; Installation tasks
Name: desktopicon; Description: "Create a &desktop shortcut";    Flags: checkedonce
Name: menuicon;    Description: "Create a &start menu shortcut"; Flags: unchecked

[Icons]
; Shortcuts
Name: "{commondesktop}\White Album 2";  Filename: "{app}\WA2_en.exe"; Tasks: desktopicon
Name: "{commonprograms}\White Album 2"; Filename: "{app}\WA2_en.exe"; Tasks: menuicon

[INI]
; Edit SYSTEM.ini
Filename: "{userdocs}\Leaf\WHITE ALBUM2\SYSTEM.ini"; Section: "DEFAULT"; Key: "mov_lv"; String: 2

[Registry]
; Registry keys
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv010.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv010"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv020.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv020"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv070.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv070"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv080.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv080"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv090.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv090"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv100.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv100"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv110.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv110"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv120.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv120"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv130.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv130"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv140.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv140"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv200.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv200"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv210.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv210"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv220.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv220"
Root: HKA; Subkey: "{#WA2REGK}\WA2EN"; ValueName: "mv230.pak"; ValueType: dword; \
           ValueData: "1"; Flags: createvalueifdoesntexist; Components: "videos\mv230"

[Run]
; Postinstall options
Filename: "{app}\WA2_en.exe"; Description: "Launch game";        Flags: {#POSTINS} nowait
Filename: "{app}\novels";     Description: "Open novels folder"; Flags: {#POSTINS} unchecked shellexec; Components: novels
Filename: "{log}";            Description: "Open log file";      Flags: {#POSTINS} unchecked shellexec

[Files]
; Patch files
Source: "{#BINPATH}\en.pak";       DestDir: "{app}";           Components: patch;        Flags: setntfscompression
Source: "{#BINPATH}\mv000.pak";    DestDir: "{app}";           Components: patch;        Flags: onlyifdoesntexist
Source: "{#BINPATH}\WA2_en.exe";   DestDir: "{app}";           Components: patch;        Flags: replacesameversion

; Subtitles
Source: "{tmp}\d3d9.dll";          DestDir: "{app}";           Components: subtitles;    Flags: {#EXTERNL}; ExternalSize: {#SIZE_D3DX9}
Source: "{tmp}\font.png";          DestDir: "{app}\todokanai"; Components: subtitles;    Flags: {#EXTERNL}; ExternalSize: {#SIZE_STFNT}
Source: "{tmp}\subtitles";         DestDir: "{app}\todokanai"; Components: subtitles;    Flags: {#EXTERNL}; ExternalSize: {#SIZE_STTXT}

; Extra videos
Source: "{tmp}\mv010.pak";         DestDir: "{app}\IC";        Components: videos\mv010; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV010}
Source: "{tmp}\mv020.pak";         DestDir: "{app}\IC";        Components: videos\mv020; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV020}
Source: "{tmp}\mv070.pak";         DestDir: "{app}\IC";        Components: videos\mv070; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV070}
Source: "{tmp}\mv080.pak";         DestDir: "{app}\IC";        Components: videos\mv080; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV080}
Source: "{tmp}\mv090.pak";         DestDir: "{app}\IC";        Components: videos\mv090; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV090}
Source: "{tmp}\mv100.pak";         DestDir: "{app}";           Components: videos\mv100; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV100}
Source: "{tmp}\mv110.pak";         DestDir: "{app}";           Components: videos\mv110; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV110}
Source: "{tmp}\mv120.pak";         DestDir: "{app}";           Components: videos\mv120; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV120}
Source: "{tmp}\mv130.pak";         DestDir: "{app}";           Components: videos\mv130; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV130}
Source: "{tmp}\mv140.pak";         DestDir: "{app}";           Components: videos\mv140; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV140}
Source: "{tmp}\mv200.pak";         DestDir: "{app}";           Components: videos\mv200; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV200}
Source: "{tmp}\mv210.pak";         DestDir: "{app}";           Components: videos\mv210; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV210}
Source: "{tmp}\mv220.pak";         DestDir: "{app}";           Components: videos\mv220; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV220}
Source: "{tmp}\mv230.pak";         DestDir: "{app}";           Components: videos\mv230; Flags: {#EXTERNL}; ExternalSize: {#SIZE_MV230}

; Novels
Source: "{tmp}\{#NAME_NVL01}.pdf"; Destdir: "{app}\novels";    Components: novels;       Flags: {#EXTERNL}; ExternalSize: {#SIZE_NVL01}
Source: "{tmp}\{#NAME_NVL02}.pdf"; Destdir: "{app}\novels";    Components: novels;       Flags: {#EXTERNL}; ExternalSize: {#SIZE_NVL02}

[Code]
var
  DLPage:  TDownloadWizardPage;
  folder:  String;

{* Get the installation folder of the game *}
function GetInstallDir(var value: String): Boolean;
begin
  Result := RegQueryStringValue(HKLM, '{#WA2REGK}', 'InstallDir', value)
         or RegQueryStringValue(HKCU, '{#WA2REGK}', 'InstallDir', value);
end;

{* Prepare component file download *}
function PrepareDL(const dest, name, cmpn: String): Boolean;
var
  path: String;
begin
  Result := WizardIsComponentSelected(cmpn);
  if not Result then Exit;
  path := ExpandConstant(dest + name);
  if FileExists(path) then begin
    if FileExists(path + '.BKP') then begin
      Result := not RegValueExists(HKA, '{#WA2REGK}\WA2EN', name);
      if Result then Log('Overwriting old ' + name + '.');
    end else begin
        Result := RenameFile(path, path + '.BKP');
        if Result then Log('Created backup for '  + name + '.')
        else Log('Failed to create backup for ' + name + '.');
    end;
  end;
end;

procedure InitializeWizard();
begin
  if GetInstallDir(folder) then
    WizardForm.DirEdit.Text := folder;

  WizardForm.LicenseAcceptedRadio.Checked := True;

  DLPage := CreateDownloadPage(SetupMessage(msgWizardPreparing),
                               SetupMessage(msgPreparingDesc), nil);
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  Result := True;
  if CurPageID = wpReady then begin
    if not WizardIsComponentSelected('subtitles') and
       not WizardIsComponentSelected('videos') and
       not WizardIsComponentSelected('novels') then Exit;
    Log('-- Downloading files --');
    DLPage.Clear;

    if WizardIsComponentSelected('subtitles') then begin
      DLPage.Add('{#LINK_D3DX9}', 'd3d9.dll', '{#HASH_D3DX9}');
      DLPage.Add('{#LINK_STFNT}', 'font.png', '{#HASH_STFNT}');
      DLPage.Add('{#LINK_STTXT}', 'subtitles', '{#HASH_STTXT}');
    end else begin
      Log('Subtitles were not selected.')
    end;

    if WizardIsComponentSelected('videos') then begin
      if PrepareDL('{app}\IC\', 'mv010.pak', 'videos\mv010') then
        DLPage.Add('{#LINK_MV010}', 'mv010.pak', '{#HASH_MV010}');
      if PrepareDL('{app}\IC\', 'mv020.pak', 'videos\mv020') then
        DLPage.Add('{#LINK_MV020}', 'mv020.pak', '{#HASH_MV020}');
      if PrepareDL('{app}\IC\', 'mv070.pak', 'videos\mv070') then
        DLPage.Add('{#LINK_MV070}', 'mv070.pak', '{#HASH_MV070}');
      if PrepareDL('{app}\IC\', 'mv080.pak', 'videos\mv080') then
        DLPage.Add('{#LINK_MV080}', 'mv080.pak', '{#HASH_MV080}');
      if PrepareDL('{app}\IC\', 'mv090.pak', 'videos\mv090') then
        DLPage.Add('{#LINK_MV090}', 'mv090.pak', '{#HASH_MV090}');
      if PrepareDL('{app}\', 'mv100.pak', 'videos\mv100') then
        DLPage.Add('{#LINK_MV100}', 'mv100.pak', '{#HASH_MV100}');
      if PrepareDL('{app}\', 'mv110.pak', 'videos\mv110') then
        DLPage.Add('{#LINK_MV110}', 'mv110.pak', '{#HASH_MV110}');
      if PrepareDL('{app}\', 'mv120.pak', 'videos\mv120') then
        DLPage.Add('{#LINK_MV120}', 'mv120.pak', '{#HASH_MV120}');
      if PrepareDL('{app}\', 'mv130.pak', 'videos\mv130') then
        DLPage.Add('{#LINK_MV130}', 'mv130.pak', '{#HASH_MV130}');
      if PrepareDL('{app}\', 'mv140.pak', 'videos\mv140') then
        DLPage.Add('{#LINK_MV140}', 'mv140.pak', '{#HASH_MV140}');
      if PrepareDL('{app}\', 'mv200.pak', 'videos\mv200') then
        DLPage.Add('{#LINK_MV200}', 'mv200.pak', '{#HASH_MV200}');
      if PrepareDL('{app}\', 'mv210.pak', 'videos\mv210') then
        DLPage.Add('{#LINK_MV210}', 'mv210.pak', '{#HASH_MV210}');
      if PrepareDL('{app}\', 'mv220.pak', 'videos\mv220') then
        DLPage.Add('{#LINK_MV220}', 'mv220.pak', '{#HASH_MV220}');
      if PrepareDL('{app}\', 'mv230.pak', 'videos\mv230') then
        DLPage.Add('{#LINK_MV230}', 'mv230.pak', '{#HASH_MV230}');
    end else begin
      Log('No videos were selected.');
    end;

    if WizardIsComponentSelected('novels') then begin
      DLPage.Add('{#LINK_NVL01}', '{#NAME_NVL01}.pdf', '{#HASH_NVL01}');
      DLPage.Add('{#LINK_NVL02}', '{#NAME_NVL02}.pdf', '{#HASH_NVL02}');
    end else begin
      Log('Novels were not selected.')
    end;

    DLPage.Show;
    try
      DLPage.Download;
    except
      Result := False;
      MsgBox(GetExceptionMessage, mbCriticalError, MB_OK);
    finally
      DLPage.Hide;
    end;
  end;
end;
